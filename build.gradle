subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  repositories {
    mavenCentral()
  }

  apply plugin: 'checkstyle'

  checkstyle {
    configFile rootProject.file('checkstyle.xml')
  }

  task checkstyle(type: Checkstyle) {
    source 'src/main/java'
    ignoreFailures true
    showViolations true
    include '**/*.java'

    classpath = files()
  }

  afterEvaluate {
    if (project.tasks.findByName('check')) {
      check.dependsOn('checkstyle')
    }
  }

  buildscript {
    repositories {
      mavenCentral()
    }
  }
}

ext {
  javaVersion = 1.8
}

ext.deps = [
    // Common
    inject: 'javax.inject:javax.inject:1',

    // Compiler
    javapoet: 'com.squareup:javapoet:1.4.0',

    // Test dependencies
    junit: 'junit:junit:4.12',
    truth: 'com.google.truth:truth:0.27',
    compiletesting: 'com.google.testing.compile:compile-testing:0.7',
]
